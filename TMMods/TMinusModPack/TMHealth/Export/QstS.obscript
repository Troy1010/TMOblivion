scn QstS

; # Defined by ini
short featureBleed
float startBleedHealthPercentage
float secondsUntilDeathFromBleedHealthPercentage
short bDisallowRestWhileBleeding
short featureRegen
float startRegenHealthPercentage
float secondsUntilFullHealthFromRegenHealthPercentage
short bPrintToConsole
short bShouldLogWarning
short bShouldLogDebug
short bShouldLogFlooding
float neutralizefGlobalTimeMultiplier
;
short bInit
float startBleedHealthFraction
float startRegenHealthFraction
ref rTemp
int counter
ref rTemp2 ; Is this variable still necessary?

begin _GameMode
	Call Init
	Let counter := 0
	Set rTemp to GetFirstRef
	while rTemp
		if eval !(rTemp.Call CFIsCallingObjectStable)
			if eval bShouldLogFlooding
				Call LogTH "Skipping bc rTemp is not stable. rTemp:" +$rTemp
			endif
			Set rTemp to GetNextRef
			continue
		endif
		if eval !(rTemp.IsActor)
			if eval bShouldLogFlooding
				Call LogTH "Skipping bc rTemp was not an actor. rTemp:" +$rTemp
			endif
			Set rTemp to GetNextRef
			continue
		endif
		if eval (rTemp.GetItemCount TMHealthToken) > 0
			if eval bShouldLogFlooding
				Call LogTH "Skipping bc rTemp already has token. rTemp:" +$rTemp
			endif
			Set rTemp to GetNextRef
			continue
		endif
		if eval (rTemp.GetDead)
			if eval bShouldLogFlooding
				Call LogTH "Skipping bc rTemp is dead. rTemp:" +$rTemp
			endif
			Set rTemp to GetNextRef
			continue
		endif
		Let counter += 1
		if eval bShouldLogFlooding
			Call LogTH "Adding item to:" + $(rTemp)
		endif
		rTemp.AddItemNS TMHealthToken 1

		Set rTemp to GetNextRef
	loop
	if eval bShouldLogFlooding
		Call LogTH "Loop complete. counter:" +$counter
	endif
end