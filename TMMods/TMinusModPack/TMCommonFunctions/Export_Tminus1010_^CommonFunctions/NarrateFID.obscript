scn NarrateFID

;**Don't forget to box the argument! (Box me with &)
;FID:FormID

array_var bvBoxedVariable ;bvBoxedVariable. (Box me with &)

string_var svTypeOf

Begin Function {bvBoxedVariable}
	; Filter
	if eval !bvBoxedVariable
		SetFunctionValue "Narrate\Error. Narrate was passed an UnconstructedCollection by "+$GetCallingScript+". Make sure to box Narrate's arguments with a &."
		return
	endif
	;
	let svTypeOf := TypeOf *bvBoxedVariable
	if eval Call IsCollection bvBoxedVariable ;Proper syntax would be (&(*bvBoxedVariable))
		SetFunctionValue "<Collection>  Size:"+$(ar_Size *bvBoxedVariable)+"..%r"+Call zCFzFnNarrateCollection *bvBoxedVariable 1 0
	elseif eval svTypeOf == "String" || svTypeOf == "Number"
		SetFunctionValue $(*bvBoxedVariable)
	elseif eval svTypeOf == "Form"
		SetFunctionValue Call zCFzFnNarrateForm (*bvBoxedVariable) 1
	elseif eval svTypeOf == "<Bad Array>"
		SetFunctionValue "<Collection(Unconstructed)>"
	else
		SetFunctionValue "<Unknown>"
	endif
	; Close
	sv_Destruct svTypeOf
end