scn QstS

short bFeatureUnlock
short bFeaturePrintMenuIdentifiers
short bFeatureTCLStart
short bFeatureSpeedyPlayer
short bFeatureTransport
short bFeatureStartRunning
short bFeatureStartNewGame
short bQuitOnStartIfSpaceDown
short bStartMode1
short bStartMode2
short bStartMode_Continue

float fQuestDelayTime
ref rCrosshairRef

string_var svPosComplete
array_var Iter

short bClickNextContinue
short bClickNextNewGame
short bClickNextYes
short bExitNextChargenMenu
short bSkipIntroVideo

short bNextSpacebarTCL
float fCloseAllMenusFrameTimer
short bStartTeleportToSpot1
float fTeleportToSpot1Timer
ref rTemp

begin _GameMode
	if eval bSkipIntroVideo
		TapKey 1
		Let bSkipIntroVideo := 0
	endif
	if eval bFeatureUnlock
		Let rCrosshairRef := GetCrosshairRef
		if eval rCrosshairRef
			if eval rCrosshairRef.GetLocked
				rCrosshairRef.UnLock
			endif
		endif
	endif
	if eval bFeatureTransport
		if eval (OnKeyDown 64) && (IsKeyPressed3 42) ;F6, LShift
			Call Logd "PlayerPosition"+(Call NarrateSL &(Call GetPosComplete PlayerRef))
		endif
		if eval (OnKeyDown 65) && (IsKeyPressed3 42) ;F7, LShift
			Call SetPosComplete PlayerRef ar_List 47073.3 82958.1 294.203 47.0626 Tamriel
		endif
		if eval (OnKeyDown 66) && (IsKeyPressed3 42) ;F8, LShift
		endif
		if eval (OnKeyDown 67) && (IsKeyPressed3 42) ;F9, LShift
		endif
	endif
	if eval bNextSpacebarTCL
		if eval (OnKeyDown 57) ;Spacebar
			Let bNextSpacebarTCL := 0
			con_TCL
			Call Delay -30 WaiterEnableSpacebar ar_Null ;In a waiter so that player doesn't jump
		endif
	endif
	if eval bStartTeleportToSpot1
		Let fTeleportToSpot1Timer := 10
	endif
	if eval fTeleportToSpot1Timer >= 0
		Let fTeleportToSpot1Timer -= GetSecondsPassed
		if eval fTeleportToSpot1Timer < 0
			Logd "Teleporting to spot 1.."
			Call SetPosComplete PlayerRef ar_List 47073.3 82958.1 294.203 47.0626 Tamriel
			; TODO: Shouldn't do this if not Oblivion
			; Set the stage of CharGen forward so that it doesn't teleport us back to the original start.
			SetStage (Call GetFormFromID "0002466E") 10
		endif
	endif
end

begin _MenuMode
	if eval MenuMode 1044 ;MainMenu
		if eval bClickNextNewGame
			Let bClickNextNewGame := 0
			ClickMenuButton "#3" 1044
		endif
		if eval bClickNextContinue
			Let bClickNextContinue := 0
			ClickMenuButton "#2" 1044
		endif
	elseif eval MenuMode 1001 ;BasicMessage
		if eval bClickNextYes
			Let bClickNextYes := 0
			ClickMenuButton "#4" 1001
		endif
	elseif eval MenuMode 1036 ;ChargenMenu
		if eval bExitNextChargenMenu
			Let bExitNextChargenMenu := 0
			Let fCloseAllMenusFrameTimer := 10
		endif
		if eval fCloseAllMenusFrameTimer >= 0
			Let fCloseAllMenusFrameTimer -= 1
			if eval fCloseAllMenusFrameTimer < 0
				CloseAllMenus
				ToggleFirstPerson 1
				PlayerRef.SetName "Mr. NoName"
				if eval bStartMode2
					PlayerRef.SetAVC avcMarksman 25
					PlayerRef.SetAVC avcConjuration 25
					PlayerRef.SetAVC avcAthletics 25
					PlayerRef.SetAVC avcAcrobatics 25
					PlayerRef.SetAVC avcBlade 25
					PlayerRef.SetAVC avcBlock 25
					PlayerRef.ModAVModC avcHealth "max" 50
					PlayerRef.AddSpell GetFormFromMod (GetNthModName 0) "0AA05F"
					PlayerRef.AddSpell GetFormFromMod (GetNthModName 0) "0A97B3"
					PlayerRef.AddSpell GetFormFromMod (GetNthModName 0) "0A9825"
					PlayerRef.AddSpell GetFormFromMod (GetNthModName 0) "0A9826"
					PlayerRef.AddSpell GetFormFromMod (GetNthModName 0) "0A97AD"
					PlayerRef.AddSpell GetFormFromMod (GetNthModName 0) "0A981A"
					PlayerRef.AddItem GetFormFromMod (GetNthModName 0) "00000f" 1000
					PlayerRef.AddItem GetFormFromMod (GetNthModName 0) "03A860" 1
					PlayerRef.AddItem GetFormFromMod (GetNthModName 0) "017829" 25
				endif
				if eval bStartMode1 || bStartMode2
					Let bStartTeleportToSpot1 := 1
				endif
			endif
		endif
	endif
end