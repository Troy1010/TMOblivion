scn QstS

short logLevel
short bEnableBleedAndRegen
float startBleedHealthPercentage
float startRegenHealthPercentage
float secondsUntilDeathFromBleedHealthPercentage
float secondsUntilFullHealthFromRegenHealthPercentage
float secondsMult

float startBleedHealthFraction
float startRegenHealthFraction

ref rTemp
int counter
ref rTemp2

begin _GameMode
	if eval GetGameLoaded
		RunBatchScript "Data\ini\TMHealth.ini"
		SetModLogLevel logLevel
		Let startBleedHealthFraction := startBleedHealthPercentage / 100
		Let startRegenHealthFraction := startRegenHealthPercentage / 100
		Logd "logLevel:" + $logLevel
		Logd "bEnableBleedAndRegen:" + $bEnableBleedAndRegen
		Logd "startBleedHealthFraction:" + $startBleedHealthFraction
		Logd "startRegenHealthFraction:" + $startRegenHealthFraction
		Logd "secondsUntilDeathFromBleedHealthPercentage:" + $secondsUntilDeathFromBleedHealthPercentage
		Logd "secondsUntilFullHealthFromRegenHealthPercentage:" + $secondsUntilFullHealthFromRegenHealthPercentage
		Logd "secondsMult:" + $secondsMult
	endif
	Let counter := 0
	Set rTemp to GetFirstRef
	while rTemp
		if eval !(rTemp.Call CFIsCallingObjectStable)
			if eval ShouldLog LogLevelFlooding
				Logf "Skipping bc rTemp is not stable. rTemp:" +$rTemp
			endif
			Set rTemp to GetNextRef
			continue
		endif
		if eval !(rTemp.IsActor)
			if eval ShouldLog LogLevelFlooding
				Logf "Skipping bc rTemp was not an actor. rTemp:" +$rTemp
			endif
			Set rTemp to GetNextRef
			continue
		endif
		if eval (rTemp.GetItemCount TMHealthToken) > 0
			if eval ShouldLog LogLevelFlooding
				Logf "Skipping bc rTemp already has token. rTemp:" +$rTemp
			endif
			Set rTemp to GetNextRef
			continue
		endif
		if eval (rTemp.GetDead)
			if eval ShouldLog LogLevelFlooding
				Logf "Skipping bc rTemp is dead. rTemp:" +$rTemp
			endif
			Set rTemp to GetNextRef
			continue
		endif

		; TODO: Add to player silently
;		if eval (rTemp == PlayerRef)
;			;PlayerRef.AddItem shows a message at the top left of the screen.
;			;This workaround prevents that message from being shown.
;			Let rTemp2 := PlayerRef.PlaceAtMe TMHealthToken 1
;			rTemp2.Activate PlayerRef 1
;			Set rTemp to GetNextRef
;			continue
;		endif

		Let counter += 1
		if eval bEnableBleedAndRegen
			if eval ShouldLog LogLevelFlooding
				Logf "Adding item to:" + $(rTemp)
			endif
			rTemp.AddItem TMHealthToken 1
		endif

		Set rTemp to GetNextRef
	loop
	if eval (ShouldLog LogLevelFlooding)
		Logf "Loop complete. counter:" +$counter
	endif
end